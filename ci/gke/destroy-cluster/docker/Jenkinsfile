pipeline {
    // @see https://www.jenkins.io/doc/book/pipeline/syntax/#agent-parameters
    agent any

    environment {
        CLOUDSDK_CONFIG = "${env.WORKSPACE}"
        MODULE_FOLDER = "clusters/gke"
        MODULE_ROOT = "terraform/$MODULE_FOLDER"
    }

    parameters {
        string(name: 'REQUEST_TOKEN', description: 'Originating pipeline job id.  Appended to the MODULE_FOLDER environment variable.')
        string(name: 'READWRITE_BUCKET_CREDENTIALS_ID', description: 'Google Cloud Storage service account with [ Storage Object Admin ] permissions')
        string(name: 'CREDENTIALS_BUCKET', description: 'Google Cloud Storage bucket that holds service account with [ Kubernetes Cluster Admin ] permissions')
        string(name: 'SECRETS_BUCKET', description: 'Google Cloud Storage bucket that holds [ backend.tf ] among other sensitive configuration files')
        string(name: 'VARS_BUCKET', description: 'Google Cloud Storage bucket that holds [ terraform.tfvars ]')
    }

    stages {

        stage("clone source") {
            steps {
                cleanWs()
                // TODO consider replacing with [checkout scm] as described here: https://support.cloudbees.com/hc/en-us/articles/226122247-How-to-Customize-Checkout-for-Pipeline-Multibranch-
                git branch: "main", url: "https://github.com/pacphi/docker-terraform-and-jenkins.git"
            }
        }

        stage("fetch sensitive configuration") {
            agent {
                // @see https://stackoverflow.com/questions/44206339/jenkins-declarative-pipeline-docker-registry if you would like to adapt the params below to source container image from private registry
                docker {
                    image 'gcr.io/google.com/cloudsdktool/cloud-sdk:latest'
                    args  '--rm -e HOME=/tmp'
                    reuseNode true
                }
            }
            steps {
                // this service account has only permissions to read from cloud storage buckets
                // @see https://stackoverflow.com/questions/45355007/how-to-authenticate-with-a-google-service-account-in-jenkins-pipeline
                withCredentials([file(credentialsId: "$READWRITE_BUCKET_CREDENTIALS_ID", variable: 'JSON_KEY')]) {
                    dir("${env.MODULE_ROOT}") {
                        sh('cp -f ${JSON_KEY} gcp-service-account.json')
                        // emit details of container image we're employing
                        sh('gcloud version')
                        // authentication and activation of the service account that will read (and download files) from cloud storage buckets
                        sh('gcloud auth activate-service-account --key-file=gcp-service-account.json')
                        // @see https://cloud.google.com/storage/docs/gsutil/commands/cp
                        // user-supplied configuration that will drive terraform module
                        // this configuration would have been defined in an upstream parameterized pipeline (with defaults) whose duty it is to upload to the bucket
                        // parameters are per job resulting in idempotent creation of resources
                        sh("gsutil cp $VARS_BUCKET/$MODULE_FOLDER/$REQUEST_TOKEN/terraform.tfvars .")
                        // sets up where we will manage terraform state
                        // the backend subpath is user supplied in upstream parameterized pipeline, and is married with operator defined constants, only the subpath will vary per job 
                        sh("gsutil cp $SECRETS_BUCKET/$MODULE_FOLDER/$REQUEST_TOKEN/backend.tf .")
                        // this service account differs from $JENKINS_CREDENTIALS_ID in that it will have elevated permissions to create resources as defined in terraform module; overwrites prior copy of service account file
                        sh("gsutil cp $CREDENTIALS_BUCKET/gcp-service-account.json .")
                    }
                }
            }
        }

        stage("invoke terraform") {
            agent {
                // @see https://stackoverflow.com/questions/55930244/jenkinsfile-to-run-terraform
                // @see https://stackoverflow.com/questions/44206339/jenkins-declarative-pipeline-docker-registry if you would like to adapt the params below to source container image from private registry
                docker {
                    image 'hashicorp/terraform:light'
                    args  '--rm -e HOME=/tmp --entrypoint='
                    reuseNode true
                }
            }
            steps {
                dir("${env.MODULE_ROOT}") {
                    sh('terraform init')
                    sh('terraform destroy -auto-approve')
                }
            }
        }
        
        stage("erase ~/.kube/config for cluster") {
            agent {
                // @see https://stackoverflow.com/questions/44206339/jenkins-declarative-pipeline-docker-registry if you would like to adapt the params below to source container image from private registry
                docker {
                    image 'gcr.io/google.com/cloudsdktool/cloud-sdk:latest'
                    args  '--rm -e HOME=/tmp'
                    reuseNode true
                }
            }
            steps {
                withCredentials([file(credentialsId: "$READWRITE_BUCKET_CREDENTIALS_ID", variable: 'JSON_KEY')]) {
                    dir("${env.MODULE_ROOT}") {
                        // remove ./kube/config from secrets cloud storage bucket
                        sh("gsutil rm -f $SECRETS_BUCKET/config")
                    }
                }
            }
        }
    }
    
    post {
        // Always runs. And it runs before any of the other post conditions.
        always {
            // Wipe out the workspace before we finish!
            deleteDir()
        }
    }
    
    // The options directive is for configuration that applies to the whole job.
    options {
        // Make sure we only keep 10 builds at a time, so we don't fill up our storage!
        buildDiscarder(logRotator(numToKeepStr:'10'))
        
        // And we'd really like to be sure that this build doesn't hang forever, so
        // let's time it out after 60 minutes.
        timeout(time: 60, unit: 'MINUTES')
    }
}